plugins {
    id 'com.android.application'
    id "org.hidetake.ssh" version "2.11.2"
}

android {
    namespace 'com.tungsten.fcl'
    compileSdk 34

    def curseApiKey = System.getenv("CURSE_API_KEY")
    def oauthApiKey = System.getenv("OAUTH_API_KEY")
    if (curseApiKey == "" || oauthApiKey == "") {
        curseApiKey = "\u0024\u0032\u0061\u0024\u0031\u0030\u0024\u0066\u0067\u006a\u0058\u006b\u0078\u0030\u0030\u0062\u005a\u005a\u0035\u0079\u0070\u004d\u004f\u004e\u0069\u0063\u002e\u0055\u0075\u0037\u002f\u0042\u0065\u0031\u004b\u0046\u006a\u006d\u004d\u0063\u0070\u0072\u006b\u007a\u0065\u006c\u0048\u0068\u006a\u006a\u0073\u0037\u0046\u0045\u0072\u0058\u0074\u0037\u0069\u0036"
        oauthApiKey = "d903cc0e-c3b4-4a3c-b347-06c2e6269be0"
    }

    signingConfigs {
        DebugKey {
            storeFile file("../debug-key.jks")
            storePassword "FCL-Debug"
            keyAlias "FCL-Debug"
            keyPassword "FCL-Debug"
        }
    }

    defaultConfig {
        aaptOptions{
            ignoreAssets "!.svn:!.git:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*.scc:*~"
            ignoreAssetsPattern "!.svn:!.git:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*.scc:*~"
        }
        applicationId "com.tungsten.fcl"
        minSdk 26
        targetSdk 34
        versionCode 1151
        versionName "1.1.5.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            initWith debug
            signingConfig signingConfigs.DebugKey
        }
        configureEach {
            resValue "string", "app_version", "${defaultConfig.versionName}"
            resValue "string", "curse_api_key", curseApiKey
            resValue "string", "oauth_api_key", oauthApiKey
        }
        configureEach {
            resValue "string", "app_version", "${defaultConfig.versionName}"
        }
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def abi = output.getFilter(ABI)
            if (abi == null) {
                abi = "all"
            }
            variant.mergeAssetsProvider.get().doLast {
                def arch = System.getProperty("arch", "all")
                def assetsDir = mergeAssetsProvider.get().outputDir.get()
                def java = ['jre8', 'jre11', 'jre17', 'jre21']
                println(arch)
                java.forEach { String str ->
                    def runtimeDir = new File([assetsDir, "app_runtime/java/" + str].join(File.separator))
                    println(runtimeDir)
                    def files = fileTree(dir: runtimeDir, include: ['*.tar.xz'])
                    files.forEach { File file ->
                        if (arch != 'all' && !file.getName().contains(arch) && !file.getName().contains("universal")) {
                            print("delete:${file} :")
                            println(delete(file))
                        }
                    }
                }
            }
            outputFileName = "FCL-${variant.buildType.name}-${defaultConfig.versionName}-${abi}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }
    splits {
        def arch = System.getProperty("arch", "all")
        if (arch != 'all') {
            abi {
                enable true
                reset()
                switch (arch) {
                    case 'arm':
                        include 'armeabi-v7a'
                        break
                    case 'arm64':
                        include 'arm64-v8a'
                        break
                    case 'x86':
                        include 'x86'
                        break
                    case 'x64':
                        include 'x86_64'
                        break
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':FCLCore')
    implementation project(path: ':FCLLibrary')
    implementation project(path: ':FCLauncher')
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'org.apache.commons:commons-compress:1.25.0'
    implementation 'org.tukaani:xz:1.9'
    implementation 'com.github.steveice10:opennbt:1.5'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

ssh.settings {
    knownHosts = allowAnyHosts
}

remotes {
    try {
        Properties prop = new Properties()
        prop.load(new FileInputStream("${rootDir}/local.properties"))
        debugServer {
            host = prop.getProperty("sftp.host")
            port = prop.getProperty("sftp.port").toInteger()
            user = prop.getProperty("sftp.username")
            password = prop.getProperty("sftp.password")
        }
    } catch (Exception ignored) {

    }
}
